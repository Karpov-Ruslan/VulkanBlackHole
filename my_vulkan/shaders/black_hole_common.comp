#include "black_hole.in"

// Black Hole params
// Black hole center is (0.0, 0.0, 0.0)
const float INV_BLACK_HOLE_RADIUS = 1.0F/BLACK_HOLE_RADIUS;

#ifdef RAY_MARCHING

const float INNER_RADIUS_OF_ACCRETION_DISK = 3.0F*BLACK_HOLE_RADIUS;
const float OUTER_RADIUS_OF_ACCRETION_DISK = 6.0F*BLACK_HOLE_RADIUS;
const vec3 ROTATION_AXIS_OF_ACCRETION_DISK = normalize(vec3(0.0F, 0.0F, 4.0F));
const vec3 COLOR_OF_ACCRETION_DISK = vec3(1.0F, 0.5F, 0.0F) * BLACK_HOLE_RADIUS * 300.0F;
const float THICKNESS_OF_ACCRETION_DISK = 0.01F;
const float INV_THICKNESS_OF_ACCRETION_DISK = 1.0F / THICKNESS_OF_ACCRETION_DISK;

// Ray Marching params
const uint MAX_STEPS = 10000U;
const float h = 0.003F;

#endif // RAY_MARCHING

// Camera params
const float HALF_FOV_HORIZONTAL_TAN = tan(radians(60));

layout(local_size_x = LOCAL_SIZE_X, local_size_y = LOCAL_SIZE_Y, local_size_z = 1U) in;

layout(set = 0, binding = BINDING_FINAL_IMAGE, rgba8) uniform restrict writeonly image2D outImage;
layout(set = 0, binding = BINDING_CUBE_MAP) uniform samplerCube spaceCubeMap;

#ifdef PRECOMPUTED

layout(set = 0, binding = BINDING_PRECOMPUTED_TEXTURE) uniform sampler2D precomputedTexture;

#endif // PRECOMPUTED

layout(push_constant) uniform PushConst {
    layout(offset = PUSH_CONSTANT_CAMERA_POSITION_OFFSET)  vec3 cameraPos;
    layout(offset = PUSH_CONSTANT_CAMERA_DIRECTION_OFFSET) vec3 cameraDir;
};

// Give cameraDir for each pixel
vec3 initializeStartGrid() {
    vec2 resolution = vec2(gl_NumWorkGroups.xy * gl_WorkGroupSize.xy);

    vec2 uv = (vec2(gl_GlobalInvocationID.xy) + 0.5F)/resolution;
    vec2 xy = fma(uv, vec2(2.0F), vec2(-1.0F));

    const float horizontalScale = HALF_FOV_HORIZONTAL_TAN;
    float verticalScale = (resolution.y/resolution.x)*HALF_FOV_HORIZONTAL_TAN;

    vec3 horizontalCameraVec = horizontalScale*normalize(vec3(cameraDir.y, -cameraDir.x, 0.0));
    vec3 verticalCameraVec = verticalScale*normalize(cross(horizontalCameraVec, cameraDir));

    return cameraDir + horizontalCameraVec*xy.x + verticalCameraVec*xy.y;
}

// u = 1/r; r - radius
// uInfo = vec2(u, d(u)/d(phi));
void transformUInfoIntoDirectionAndPosition(vec2 uInfo, float phi, vec3 rotationAxis, out vec3 position, out vec3 direction) {
    vec3 normCameraPos = normalize(cameraPos);
    vec3 normRotationAxis = normalize(rotationAxis);
    vec3 normPerpendicular = cross(normRotationAxis, normCameraPos); // Normalized Perpendicular
    vec3 normPosition = normCameraPos * cos(phi) + normPerpendicular * sin(phi);
    float radius = (1.0F/uInfo.x);
    position = normPosition*radius;
    direction = normalize(cross(normRotationAxis, normPosition) - normPosition*uInfo.y*radius);
}

#ifdef PRECOMPUTED

float SampleAngle(vec2 uInfo) {
    if (uInfo.x > INV_BLACK_HOLE_RADIUS) {
        return PRECOMPUTED_TEXTURE_SPECIAL_VALUE;
    }

    const float inv_pi = 0.318309886183790671538F;
    return texture(precomputedTexture, vec2(uInfo.x*BLACK_HOLE_RADIUS, fma(atan(-uInfo.y*length(cameraPos)), inv_pi, 0.5F))).r;
}

#endif

#ifdef RAY_MARCHING

float rand(float n) {
    return fract(sin(n) * 43758.5453123F);
}

float noise(float p) {
	float fl = floor(p);
    float fc = fract(p);
	return mix(rand(fl), rand(fl + 1.0F), fc);
}

float accretionDiskDensity(vec3 position) {
    float a = -dot(position, ROTATION_AXIS_OF_ACCRETION_DISK);
    if (abs(a) > THICKNESS_OF_ACCRETION_DISK) {return 0.0F;} // Save performance
    float r = length(position + a*ROTATION_AXIS_OF_ACCRETION_DISK);
    if (r < INNER_RADIUS_OF_ACCRETION_DISK || r > OUTER_RADIUS_OF_ACCRETION_DISK) {return 0.0F;} // Save performance
    return max(noise(r*400.0F) - abs(a)*INV_THICKNESS_OF_ACCRETION_DISK, 0.0F);
}

#include "black_hole_runge_kutte.glsl"

#endif // RAY_MARCHING

vec3 traceRayBlackHole(vec3 pixelCameraDir) {
    // u = 1/r; r - radius
    // uInfo = vec2(u, d(u)/d(phi));
    float invInitialRadius = 1.0F/length(cameraPos);
    vec3 rotationAxis = cross(cameraPos, pixelCameraDir);
    vec2 uInfo = vec2(invInitialRadius, -(dot(cameraPos, pixelCameraDir)/length(rotationAxis))*invInitialRadius);
    float phi = 0.0F;

    // Forward declaration
    vec3 direction = pixelCameraDir;
    vec3 position = cameraPos;
    vec3 outputColor = vec3(0.0F);

#ifdef RAY_MARCHING

    for (uint i = 0U; i < MAX_STEPS; ++i) {
        // Case: Fall into black hole
        if (uInfo.x > INV_BLACK_HOLE_RADIUS) {
            return outputColor;
        }

        // Case: The ray is already flying in a straight line
        if (uInfo.y < -TAN_STOP_ITER*uInfo.x) {
            break;
        }

        uInfo = rk4(uInfo, h);
        phi += h;

        transformUInfoIntoDirectionAndPosition(uInfo, phi, rotationAxis, position, direction);
        outputColor += accretionDiskDensity(position)*COLOR_OF_ACCRETION_DISK*h;
    }

    // Case: Go into infinity
    transformUInfoIntoDirectionAndPosition(uInfo, phi, rotationAxis, position, direction);
    outputColor += texture(spaceCubeMap, direction).rgb;
    return outputColor;

#elif defined(PRECOMPUTED)

    phi = SampleAngle(uInfo);

    if (isnan(phi) || phi > 9.0F) {
        return vec3(0.0F);
    }

    transformUInfoIntoDirectionAndPosition(uInfo, phi, rotationAxis, position, direction);
    return texture(spaceCubeMap, position).rgb;

#endif // RAY_MARCHING, PRECOMPUTED

}

void main() {
    vec3 pixelCameraDir = initializeStartGrid();
    imageStore(outImage, ivec2(gl_GlobalInvocationID.xy), vec4(traceRayBlackHole(pixelCameraDir), 1.0F));
}